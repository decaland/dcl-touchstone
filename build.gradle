plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

group = 'com.github.decaland.touchstone'

ext {
    isLocal = project.hasProperty('local')
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    testImplementation("junit:junit:${property('dcl.version.junit')}")

    implementation "org.springframework.boot:spring-boot-gradle-plugin:${property('dcl.version.spring-boot')}"
    implementation "io.spring.gradle:dependency-management-plugin:${property('dcl.version.spring-dependency-management')}"
    implementation "org.jetbrains.kotlin:kotlin-gradle-plugin:${property('dcl.version.kotlin')}"
    implementation "org.jetbrains.kotlin:kotlin-allopen:${property('dcl.version.kotlin')}"

    implementation "io.freefair.gradle:lombok-plugin:${property('dcl.version.lombok')}"
}

publishing {
    repositories {
        maven {
            if (isLocal) {
                url = "$rootDir/../mvn-repo"
            } else {
                url = version.endsWith('SNAPSHOT')
                        ? property('dcl.repository.maven.snapshots')
                        : property('dcl.repository.maven.releases')
                credentials {
                    username = property('dcl.repository.maven.username')
                    password = property('dcl.repository.maven.password')
                }
            }
        }
    }
}

gradlePlugin {
    plugins {
        decalandLibraryJava {
            id = 'dcl-lib-java'
            displayName = 'Decaland library written in Java'
            description = 'Decaland library written in Java'
            implementationClass = 'com.github.decaland.touchstone.plugins.DecalandLibraryJava'
        }
        decalandLibraryJavaKotlin {
            id = 'dcl-lib-java-kotlin'
            displayName = 'Decaland library written in Java or Kotlin or both'
            description = 'Decaland library written in Java or Kotlin or both'
            implementationClass = 'com.github.decaland.touchstone.plugins.DecalandLibraryJavaKotlin'
        }
        decalandSpringBootLibraryJava {
            id = 'dcl-boot-lib-java'
            displayName = 'Decaland Spring Boot-based library written in Java'
            description = 'Decaland Spring Boot-based library written in Java'
            implementationClass = 'com.github.decaland.touchstone.plugins.DecalandSpringBootLibraryJava'
        }
        decalandSpringBootLibraryJavaKotlin {
            id = 'dcl-boot-lib-java-kotlin'
            displayName = 'Decaland Spring Boot-based library written in Java or Kotlin or both'
            description = 'Decaland Spring Boot-based library written in Java or Kotlin or both'
            implementationClass = 'com.github.decaland.touchstone.plugins.DecalandSpringBootLibraryJavaKotlin'
        }
        decalandSpringBootApplicationJava {
            id = 'dcl-boot-app-java'
            displayName = 'Decaland Spring Boot-based application written in Java'
            description = 'Decaland Spring Boot-based application written in Java'
            implementationClass = 'com.github.decaland.touchstone.plugins.DecalandSpringBootApplicationJava'
        }
        decalandSpringBootApplicationJavaKotlin {
            id = 'dcl-boot-app-java-kotlin'
            displayName = 'Decaland Spring Boot-based application written in Java or Kotlin or both'
            description = 'Decaland Spring Boot-based application written in Java or Kotlin or both'
            implementationClass = 'com.github.decaland.touchstone.plugins.DecalandSpringBootApplicationJavaKotlin'
        }
        decalandEssentialDependencies {
            id = 'dcl-essential-dependencies'
            displayName = 'Decaland essential library dependencies'
            description = 'Decaland essential library dependencies'
            implementationClass = 'com.github.decaland.touchstone.plugins.DecalandEssentialDependencies'
        }
    }
}
